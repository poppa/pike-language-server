; # Pike syntax specification
;
; The source of truth for this is language.yacc
; (https://github.com/pikelang/Pike/blob/master/src/language.yacc) of the Pike
; source, or more specifically "y.output" which is the generated grammar from
; language.yacc.
;
; Since the Yacc stuff genratates C code, some grammar isn't applicable (at
; least it doesn't seem so to me) when parsing Pike code statically, so there
; might be some cleaning up to do here, specifically the "error" cases below,
; which I can't see have anything to do with Pike syntax itself, but is a way
; to generate errors for the real runtime and compiler.

all = program
program = program def
        / program ";"
        / ; empty

real_string_or_identifier = identifier
                          / real_string_constant

optional_rename_inherit = ":" real_string_or_identifier
                        / ; empty

low_program_ref = expr0
inherit_ref = low_program_ref
inheritance = modifiers "inherit" inherit_ref optional_rename_inherit ";"
import = "import" constant_expr ";"
constant_name = identifier "=" expr0
constant_list = constant_name
              / constant_list "," constant_name
constant = modifiers "constant" constant_list ";"
block_or_semi = block
              / ";"
optional_constant = ; empty
                  / "constant"

; FIXME: These "identifiers" should probably not be quoted
def = modifiers optional_attributes full_type optional_constant identifier "(" arguments ")" block_or_semi
    / modifiers optional_attributes full_type optional_constant name_list ";"
    / modifiers "{" program "}"
    / modifiers named_class
    / modifiers enum
    / inheritance
    / import
    / constant
    / annotation ";"
    ; Does this make any sense? I'm guessing not
    ; / "@" "constant" ";"
    ; Why isn't this the same as enum above for instance?
    / typedef
    / static_assertion ";"

static_assertion = "_Static_assert" "(" expr0 "," expr0 ")"

optional_dot_dot_dot = "..."
                     / ; empty

optional_identifier = identifier
                    / ; empty

new_arg_name = full_type optional_dot_dot_dot optional_identifier

func_args = "(" arguments ")"

arguments = optional_comma
          / arguments2 optional_comma

arguments2 = new_arg_name
           / arguments2 "," new_arg_name
           / arguments2 ":" new_arg_name

modifier = "__unused__"
         / "__weak__"
         / "continue"
         / "extern"
         / "final"
         / "inline"
         / "local"
         / "optional"
         / "private"
         / "protected"
         / "public"
         / "static"
         / "variant"

magic_identifiers1 = "__unused__"
                   / "__weak__"
                   / "_Static_assert"
                   / "extern"
                   / "final"
                   / "inline"
                   / "local"
                   / "optional"
                   / "private"
                   / "protected"
                   / "public"
                   / "static"
                   / "variant"

magic_identifiers2 = "__attribute__"
                   / "__deprecated__"
                   / "__func__"
                   / "array"
                   / "enum"
                   / "float"
                   / "function"
                   / "int"
                   / "mapping"
                   / "mixed"
                   / "multiset"
                   / "object"
                   / "program"
                   / "string"
                   / "typedef"
                   / "void"

magic_identifiers3 = "if"
                   / "do"
                   / "for"
                   / "while"
                   / "else"
                   / "foreach"
                   / "catch"
                   / "gauge"
                   / "class"
                   / "break"
                   / "case"
                   / "constant"
                   / "continue"
                   / "default"
                   / "import"
                   / "inherit"
                   / "lambda"
                   / "predef"
                   / "return"
                   / "sscanf"
                   / "switch"
                   / "typeof"
                   / "global"

magic_identifiers = magic_identifiers1
                  / magic_identifiers2
                  / magic_identifiers3

magic_identifier = identifier
                 / "reserved identifier"
                 / magic_identifiers

annotation = "@" constant_expr

annotation_list = ; empty
                / annotation ":" annotation_list

modifiers = annotation_list modifier_list

modifier_list = ; empty
              / modifier_list modifier

attribute = "__attribute__" "(" string_constant optional_comma ")"
          / "__deprecated__" "(" ")"
          / "__deprecated__"

optional_attributes = ; empty
                    / optional_attributes attribute

cast = "(" type ")"

soft_cast = "[" type "]"

type2 = type
      / identifier_type

simple_type2 = type2

full_type = full_type "|" type3
          / type3

type = type "|" type3
     / basic_type

type3 = basic_type
      / identifier_type

basic_type = "float"
           / "void"
           / "mixed"
           / "auto"
           / "zero" ; I think this is comming in 8.1
           / "string" opt_string_width
           / "int" opt_int_range
           / "mapping" opt_mapping_type
           / "function" opt_function_type
           / "object" opt_program_type
           / "program" opt_program_type
           / "array" opt_array_type
           / "multiset" opt_multiset_type
           / "__attribute__" "(" string_constant "," full_type ")"
           / "__deprecated__" "(" full_type ")"

identifier_type = idents
                / typeof

number = "integer literal"
       / "-" "integer literal"

number_or_maxint = ; empty
                 / number

number_or_minint = ; empty
                 / number

expected_dot_dot = ".."
safe_int_range_type = "bits"
                    / number_or_minint expected_dot_dot number_or_maxint
                    / number

opt_int_range = ; empty
              / "(" safe_int_range_type ")"

opt_string_width = opt_int_range
                 / "(" safe_int_range_type ":" safe_int_range_type ")"
                 / "(" safe_int_range_type ":" ")"
                 / "(" ":" safe_int_range_type ")"

opt_program_type = ; empty
                 / "(" full_type ")"
                 / "(" string_constant ")"

opt_function_type = "(" function_type_list optional_dot_dot_dot ":" full_type ")"
                  / ; empty

function_type_list = optional_comma
                   / function_type_list2 optional_comma

function_type_list2 = full_type
                    / function_type_list2 "," full_type

opt_multiset_type = "(" full_type ")"
                  / ; empty

opt_array_type = "(" full_type ")"
               / "(" safe_int_range_type ":" full_type ")"
               / "(" ":" full_type ")"
               / "(" safe_int_range_type ":" ")"
               / ; empty

opt_mapping_type = "(" full_type ":" full_type ")"
                 / ; empty

name_list = new_name
          / name_list "," new_name
new_name = identifier
         /  identifier "=" expr0
new_local_name = identifier
               / identifier "=" expr0

block = "{" statements "}"

local_name_list = new_local_name
                / local_name_list "," new_local_name

constant_expr = expr0

local_constant_name = identifier "=" expr0
local_constant_list = local_constant_name
                    / local_constant_list "," local_constant_name

local_constant = "constant" local_constant_list ";"
statements = ; empty
           / statements statement

statement_with_semicolon = unused2 ";"
normal_label_statement = statement_with_semicolon
                       / import
                       / cond
                       / return
                       / local_constant
                       / block
                       / break ";"
                       / continue ";"
                       / ";"

statement = normal_label_statement
          / while
          / do
          / for
          / foreach
          / switch
          / case
          / default
          / labeled_statement
          / simple_type2 local_function
          / "continue" simple_type2 local_generator
          / implicit_modifiers named_class

labeled_statement = identifier ":" statement

optional_label = identifier
               / ; empty

break = "break" optional_label

default = "default" ":"
        / "default"

continue = "continue" optional_label

start_lambda = ; empty

implicit_identifier = ; empty

lambda = "lambda" implicit_identifier start_lambda func_args failsafe_block

local_function = identifier func_args failsafe_block

local_generator = identifier func_args failsafe_block

create_arg = modifiers full_type optional_dot_dot_dot identifier
create_arguments2 = create_arg
                  / create_arguments2 "," create_arg
                  / create_arguments2 ":" create_arg

create_arguments = optional_comma
                 / create_arguments2 optional_comma

optional_create_arguments = ; empty
                          / "(" create_arguments close_paren_or_missing

program_block = "{" program "}"
anon_class = "class" optional_create_arguments program_block
named_class = "class" identifier optional_create_arguments program_block

enum_value = ; empty
           / "=" expr0
enum_def = ; empty
         / identifier enum_value
enum_list = enum_def
          / enum_list "," enum_def
enum = "enum" optional_identifier "{" enum_list "}"
typedef = modifiers "typedef" full_type identifier ";"

cond = "if" "(" comma_expr ")" statement optional_else_part
optional_else_part = ; empty
                   / "else" statement

foreach_optional_lvalue = ; empty */
                        / lvalue
foreach_lvalues = "," lvalue
                / ";" foreach_optional_lvalue ";" foreach_optional_lvalue
foreach = "foreach" "(" expr0 foreach_lvalues ")" statement
do = "do" statement "while" "(" comma_expr ")" ";"
for = "for" "(" unused ";" for_expr ";" unused ")" statement
while = "while" "(" comma_expr ")" statement

for_expr = ; empty
         / comma_expr

switch = "switch" "(" comma_expr ")" statement

case = "case" comma_expr ":"
     / "case" comma_expr expected_dot_dot optional_comma_expr ":"
     / "case" expected_dot_dot comma_expr ":"

optional_continue = ; empty
                  / "continue"
                  / "break"
return = optional_continue "return" ";"
       / optional_continue "return" comma_expr ";"

unused = ; empty
       / comma_expr

unused2 = comma_expr

optional_comma_expr = ; empty
                    / comma_expr

comma_expr = comma_expr2
           / simple_type2 local_name_list

comma_expr2 = expr0
            / comma_expr2 "," expr0

splice_expr = expr0
            / "@" expr0

expr0 = expr01
      / expr4 assign expr0
      / "[" low_lvalue_list "]" assign expr0

expr01 = expr1
       / expr1 "?" expr01 ":" expr01

assign = "="
       / "&="
       / "|="
       / "^="
       / "<<="
       / ">>="
       / "+="
       / "-="
       / "*="
       / "**="
       / "%="
       / "/="
       / "?="

optional_comma = ; empty
               / ","

expr_list = ; empty
          / expr_list2 optional_comma

expr_list2 = splice_expr
           / expr_list2 "," splice_expr

m_expr_list = ; empty
            / m_expr_list2 optional_comma

m_expr_list2 = assoc_pair
             / m_expr_list2 "," assoc_pair

assoc_pair = expr0 ":" expr0

expr1 = expr2
      / expr1 "||" expr1
      / expr1 "&&" expr1
      / expr1 "|" expr1
      / expr1 "^" expr1
      / expr1 "&" expr1
      / expr1 "==" expr1
      / expr1 "!=" expr1
      / expr1 ">" expr1
      / expr1 ">=" expr1
      / expr1 "<" expr1
      / expr1 "<=" expr1
      / expr1 "<<" expr1
      / expr1 ">>" expr1
      / expr1 "+" expr1
      / expr1 "-" expr1
      / expr1 "*" expr1
      / expr1 "**" expr1
      / expr1 "%" expr1
      / expr1 "/" expr1

expr2 = expr3
     / cast expr2
     / soft_cast expr2
     / "++" expr4
     / "--" expr4
     / "!" expr2
     / "~" expr2
     / "-" expr2

expr3 = expr4
      / expr4 "++"
      / expr4 "--"

optional_block = ; empty
               / "{" start_lambda statements "}"

apply = expr4 "(" expr_list ")" optional_block
      / expr4 "(?" expr_list ")" optional_block

expr4 = idents
      / expr5
      / expr5 "." identifier

expr5 = literal_expr
      / catch
      / gauge
      / typeof
      / sscanf
      / static_assertion
      / lambda
      / anon_class
      / enum
      / apply
      / expr4 "[" "*" "]"
      / expr4 "[" expr0 "]"
      / expr4 "[" range_bound expected_dot_dot range_bound "]"
      / expr4 "[?" expr0 "]"
      / expr4 "[?" range_bound expected_dot_dot range_bound "]"
      / "(" comma_expr2 ")"
      / expr4 "->" magic_identifier
      / expr4 "->?" identifier

literal_expr = string
             / "integer literal"
             / "float literal"
             / "({" expr_list "})"
             / "([" m_expr_list "])"
             / "(<" expr_list ">)"

unqualified_idents = low_idents
                   / unqualified_idents "." identifier

qualified_idents = qualified_ident
                 / qualified_idents "." identifier

idents = unqualified_idents
       / qualified_idents

string_or_identifier = identifier
                     / string

inherit_specifier = string_or_identifier "::"
                 / "local" "::"
                 / "global" "::"
                 / inherit_specifier "local" "::"
                 / inherit_specifier identifier "::"

low_idents = identifier
           / "." identifier
           / "global" "." identifier
           / "reserved identifier"

qualified_ident = "predef" "::" identifier
                / "version prefix" "::" identifier
                / inherit_specifier identifier
                / "::" identifier

range_bound = ; empty
           / comma_expr
           / "<" comma_expr

gauge = "gauge" catch_arg

typeof = "typeof" "(" expr0 ")"
catch_arg = "(" comma_expr ")"
          / block

catch = "catch" catch_arg

sscanf = "sscanf" "(" expr0 "," expr0 lvalue_list ")"
lvalue = expr4
       / "[" low_lvalue_list "]"
       / type2 identifier

low_lvalue_list = lvalue lvalue_list

lvalue_list = ; /* empty */
            / "," lvalue lvalue_list

string_segment = "string literal"
               / "__func__"

string = string_segment
       / string string_segment

string_constant = string
                / string_constant "+" string

real_string_constant = "string literal"
                     / real_string_constant "string literal"
                     / real_string_constant "+" "string literal"

bad_inherit = bad_expr_ident
            / "array"
            / "__attribute__"
            / "break"
            / "case"
            / "catch"
            / "class"
            / "continue"
            / "default"
            / "__deprecated__"
            / "do"
            / "enum"
            / "float"
            / "for"
            / "foreach"
            / "function"
            / "__func__"
            / "gauge"
            / "if"
            / "import"
            / "int"
            / "lambda"
            / "mapping"
            / "mixed"
            / "multiset"
            / "object"
            / "program"
            / "return"
            / "sscanf"
            / "string"
            / "switch"
            / "typedef"
            / "typeof"
            / "void"
            / "reserved identifier"

bad_expr_ident = "inline"
               / "predef"
               / "private"
               / "protected"
               / "public"
               / "optional"
               / "variant"
               / "__weak__"
               / "static"
               / "extern"
               / "final"
               / "else"
               / "inherit"

identifier  = ("_" / ALPHA) *(ALPHA / DIGIT / "_")
